apply plugin: 'java'
apply plugin: 'spring-boot'
apply from: 'mapstruct.gradle'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.5.RELEASE")
        classpath('se.transmode.gradle:gradle-docker:1.2')
    }
}

apply from: 'docker.gradle'


jar {
    baseName = 'gamemanager'
}

repositories {
    mavenCentral()
    jcenter()
    maven{
        url="https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven{
        url="https://oss.sonatype.org/content/groups/public"
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    // tag::jetty[]
    compile project(':rest-client')
    compile("org.springframework.boot:spring-boot-starter-web") {
        exclude module: "spring-boot-starter-tomcat"
    }
    compile("org.springframework.boot:spring-boot-starter-jetty")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("org.springframework.boot:spring-boot-starter-websocket")
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
    compile("org.springframework.boot:spring-boot-autoconfigure")
    compile("org.springframework:spring-messaging")
    compile("org.springframework:spring-jms")

    compile("org.apache.activemq:activemq-broker")
    // end::jetty[]
    // tag::actuator[]
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("com.h2database:h2")
    compile("com.google.guava:guava:18.0")
    compile('io.netty:netty-all:4.0.30.Final')
    compile 'ch.qos.logback:logback-classic:1.1.6'
    runtime 'net.logstash.logback:logstash-logback-encoder:4.8'
    // end::actuator[]
    testCompile("junit:junit")
}

def ci_branch = "UNSET"
def ci_commit = "UNSET"
def ci_system = "LOCAL"
def ci_commit_short = "UNSET"
def ci_build_number = 0
if (System.getenv("TRAVIS")) {
    ci_branch = System.getenv("TRAVIS_BRANCH")
    ci_commit = System.getenv("TRAVIS_COMMIT")
    ci_system = "travis"
    ci_commit_short = System.getenv("TRAVIS_COMMIT")
    ci_build_number = System.getenv("TRAVIS_BUILD_NUMBER")
}

version = "${ci_system}_${ci_build_number}"

processResources {
    filesMatching('**/application.properties') {
        expand( COMMIT: ci_commit,
                BRANCH: ci_branch,
                COMMIT_SHORT: ci_commit_short,
                PIPELINE_COUNTER: ci_build_number,
                VERSION: project.version
        )
    }
}
